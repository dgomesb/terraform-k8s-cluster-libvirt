variable "count_server" {
  description = "Number of control plane(s)"
  type        = number
  default     = 1

  validation {
    condition     = var.count_server >= 1
    error_message = "You MUST have at least 1 Control Plane"
  }
}

variable "count_worker" {
  description = "Number of worker(s)"
  type        = number
  default     = 2
}

variable "user_passwd" {
  description = "Hashed password"
  type        = string
  default     = ""
  sensitive   = true

  validation {
    condition     = var.user_passwd != ""
    error_message = <<-EOT
      SECURITY WARNING: Empty password detected. The user will be
      able to log in without a password. To fix this, provide a hash
      generated by: openssl passwd -6 "your_password"
    EOT
  }

}

variable "shared_config" {
  description = "common variables shared among virtual machines"
  type = object({
    shared_dir    = optional(string, null) ## from locals
    dynamic_hosts = optional(string, null) ## from locals
    main_cp_ip    = optional(string, null) ## First control plane IP
    # virtual network
    vn_name = optional(string, null) ## from locals
    vn_cidr = optional(string, null) ## from locals
    # storage
    storage_name = optional(string, null) ## from locals
    base_os_name = optional(string, "noble-server-cloudimg-amd64.img")
    # userdata.tpl
    user_name       = optional(string, "ubuntu") ## User name to be created in the nodes
    user_passwd     = optional(string, "")       ## from locals (to keep it sensitive)
    user_ssh_pub    = optional(string, "")       ## PUBLIC ssh key
    timezone        = optional(string, "UTC")    ## Set the system timezone
    locale          = optional(string, "en_US")  ## Configure the system locale and apply it system-wide
    kubeadm_version = optional(string, "1.35")   ## Define major.minor and the newest patch will be installed
    # network.tpl
    net_domain = optional(string, "local.k8s") ## VMs FQDN
  })
  default = {}

  validation {
    condition     = var.shared_config.user_ssh_pub != ""
    error_message = <<-EOT
      WARNING: Setup a public ssh key is recommended. Otherwhise you will not
      be able to run 'kubctl' targeting the control plane server from your host.
    EOT
  }

}

## Storage pool

variable "storage_name" {
  description = <<-EOT
    Storage name where the volumes/disks will be created into.
    If you are not sure about the storage name on your machine,
    then run pool-list.sh within scripts folder.
  EOT
  type        = string
  default     = "default"
}

## Network settings (Virtual Network)

variable "vn_name" {
  description = "A new Virtual Network will be created"
  type        = string
  default     = "k8s"
}

variable "vn_cidr" {
  description = <<-EOT
    CIDR of the NEW virtual network. You can use net-list.sh
    within scripts folder to check current CIDRs
  EOT
  type        = string
  default     = "10.10.10.0/24"
}

variable "start_server_ip" {
  description = "When count_server > 1, the first server will get this IP"
  type        = number
  default     = 11
}

variable "start_worker_ip" {
  description = "When count_worker > 1, the first worker will get this IP"
  type        = number
  default     = 21
}

variable "pod_cidr" {
  description = "Default Flannel CIDR"
  type        = string
  default     = "10.244.0.0/16"
}
